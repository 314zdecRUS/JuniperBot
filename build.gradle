/*
 * This file is part of JuniperBotJ.
 *
 * JuniperBotJ is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.

 * JuniperBotJ is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.

 * You should have received a copy of the GNU General Public License
 * along with JuniperBotJ. If not, see <http://www.gnu.org/licenses/>.
 */
buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.at.comm_unity.gradle.plugins:jpamodelgen-plugin:1.1.4")
    }
}

allprojects {

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://dl.bintray.com/goldrenard/maven" }
        maven { url "http://nexus.bippo.co.id/nexus/content/repositories/soluvas-public-thirdparty/" } // net.reduls.sanmoku artifact
        maven { url "$rootProject.projectDir/local" }
    }

    group = 'ru.caramel'
    version = System.getenv('CI_VERSION') ?: '6.x-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'at.comm_unity.gradle.plugins.jpamodelgen'

    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:deprecation" << "--add-modules=java.xml.ws.annotation"

    ext {
        //@formatter:off

        // Common Properties
        springBootVersion                  = "${springBootVersion}"
        springCloudVersion                 = 'Greenwich.RELEASE'
        springOauthVersion                 = '2.1.0.RELEASE'
        hibernateVersion                   = '5.3.7.Final'
        lombokVersion                      = '1.18.4'
        jsonVersion                        = '20160810'
        commonsIoVersion                   = '2.5'
        commonsCollections4Version         = '4.1'
        commonsValidatorVersion            = '1.6'
        commonsCodecVersion                = '1.11'
        guavaVersion                       = '18.0'
        postgresqlVersion                  = '42.2.5'
        metricsSpringVersion               = '3.1.3'
        programJbVersion                   = '1.0.13'
        gsonVersion                        = '2.8.1'
        jodaMoneyVersion                   = '0.12'
        prettytimeVersion                  = '4.0.1.Final'
        jsoupVersion                       = '1.11.2'
        mapstructVersion                   = '1.2.0.Final'
        opencvVersion                      = '3.2.0'
        sentryVersion                      = '1.7.9'
        jacksonVersion                     = '2.9.7'

        // Integration Properties
        jdaVersion                         = '3.8.1_439'
        opusJavaVersion                    = '1.0.4' // CHECK ITS VERSION ON JDA UPDATE!
        jdaNasVersion                      = '1.0.6'
        lavaplayerVersion                  = '1.3.10'
        jbLavaplayerVersion                = '1.3.10.5'
        lavalinkVersion                    = 'fbd297512f'
        googleApiServicesYoutubeVersion    = 'v3-rev205-1.25.0'
        jwbfVersion                        = '4.0.0-SNAPSHOT'
        wikitextVersion                    = '3.1.6'
        vkSdkVersion                       = '0.5.8'
        twitch4jVersion                    = 'v0.12.0'
        romeVersion                        = '1.11.1'
        jsonApiConverter                   = '0.9'

        //@formatter:on
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:deprecation" << "--add-modules=java.xml.ws.annotation"
    }

    jpaModelgen {
        library = "org.hibernate:hibernate-jpamodelgen:${hibernateVersion}"
        jpaModelgenSourcesDir = "src/main/generated"
    }

    sourceSets {
        main {
            java {
                srcDirs += "src/main/generated"
            }
        }
    }

    dependencies {
        compileOnly(group: 'org.projectlombok', name: 'lombok', version: lombokVersion)
        annotationProcessor(group: 'org.projectlombok', name: 'lombok', version: lombokVersion)
        testCompile(group: 'org.projectlombok', name: 'lombok', version: lombokVersion)
        testAnnotationProcessor(group: 'org.projectlombok', name: 'lombok', version: lombokVersion)
    }

    dependencyManagement {
        imports { mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES) }
    }
}

ext {
    moduleName = 'JuniperBot-Parent'
}