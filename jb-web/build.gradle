import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'org.springframework.boot'

description = 'JuniperBot Discord Bot Web Application'

ext {
    moduleName = 'JuniperBot-Web'
}

bootJar {
    mainClassName = 'ru.caramel.juniperbot.web.JuniperBotApplication'
    archiveName = "JuniperBot.jar"
}

dependencies {

    compile project(':jb-core')
    compile project(':modules')

    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web')
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-security')
    compile(group: 'org.springframework.security', name: 'spring-security-acl')
    compile(group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: springOauthVersion)

    compile(group: 'org.mapstruct', name: 'mapstruct-jdk8', version: mapstructVersion)
    compile(group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstructVersion)
    annotationProcessor(group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstructVersion)

    compile(group: 'org.openpnp', name: 'opencv', version: opencvVersion + '-0')
    compile(group: 'org.opencv', name: 'opencv', version: opencvVersion)

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    testCompile(group: 'org.springframework.security', name: 'spring-security-test')

}

processResources {
    filesMatching("**/application.yml") {
        filter ReplaceTokens, tokens: [
                "build.version"     : project.version,
                "build.timestamp"   : System.currentTimeMillis() + '',
                "JDA.version"       : jdaVersion,
                "lavaplayer.version": lavaplayerVersion,
                "spring.version"    : dependencyManagement.importedProperties['spring.version']
        ]
    }
}